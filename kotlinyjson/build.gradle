apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'me.akatkov.kotlinyjson' // Change this to match your package name
version = '0.3.2' // Change this to match your version number

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "0.3.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.12'

    // Required for instrumented tests
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // TODO: figure out why removing this breaks the entire world
    implementation "org.json:json:20180813"
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

artifacts {
    archives generateSourcesJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'KotlinyJSON'
            description 'A simple JSON class that has better usage than org.JSON with JSON to Object, Object to JSON conversions.'
            url 'https://github.com/akatkov7/KotlinyJSON'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'me.akatkov.kotlinyjson'
            artifactId 'kotlinyjson'
            version '0.3.2'
        }
    }
}

def getBintrayAPIKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File("$projectDir/bintray.properties")))
    return props['API_KEY']
}

bintray {
    user = 'akatkov'
    key = getBintrayAPIKey()
    override = true
    pkg {
        repo = 'maven'
        name = 'KotlinyJSON'
        override = true

        version {
            name = '0.3.2'
            desc = 'A simple JSON class that has better usage than org.JSON with JSON to Object, Object to JSON conversions.'
            released  = new Date()
            vcsTag = '0.3.2'
        }

        licenses = ['MIT']
        vcsUrl = 'https://github.com/akatkov7/KotlinyJSON.git'
        websiteUrl = 'https://github.com/akatkov7/KotlinyJSON'
    }
    configurations = ['archives']
}

ext {
    PUBLISH_GROUP_ID = 'me.akatkov.kotlinyjson'
    PUBLISH_ARTIFACT_ID = 'kotlinyjson'
    PUBLISH_VERSION = '0.3.2'
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'